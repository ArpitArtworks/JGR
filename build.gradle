buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

import org.apache.tools.ant.filters.ReplaceTokens


allprojects {
    apply plugin: 'idea'

    repositories {
        mavenCentral()
    }
}

configure(subprojects - project(':jgr-r')) {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_1_4
    targetCompatibility = JavaVersion.VERSION_1_4

    compileJava {
        options.encoding = 'UTF-8'
    }

    javadoc {
        options.addStringOption('encoding', 'UTF-8')
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
//        archives javadocJar
    }

    dependencies {
    }
}

project(":jgr-java") {
    dependencies {
        compile project(":ibase")
        compile project(":javagd")
        compile project(":REngine")
        compile 'com.massisframework:orange-extensions:1.3.1'
    }

    jar {
        manifest {
            attributes "Implementation-Version": version
        }
    }

    shadowJar {
        baseName = 'JGR'
        classifier = null
        version = null
        dependencies {
            exclude(project(':javagd'))
            exclude(project(':rJava'))
            exclude(dependency('com.massisframework:orange-extensions:1.3.1'))
        }
    }

    jar.finalizedBy shadowJar
}

project(":jgr-r") {
    apply plugin: 'distribution'

    def javaDist = 'src/main/dist/inst/java'

    clean.doLast {
        file('src/main/dist/DESCRIPTION').delete()
        file('src/main/dist/R').deleteDir()
        file(javaDist).deleteDir()
        file(javaDist).mkdir()
    }


    task prepareJGR(type: Copy) {
        from('src/main/raw')
        into('src/main/dist')
        filter(ReplaceTokens, tokens: [VERSION: version])
    }

    distributions {
        main {
            contents {
                from {
                    'src/main/dist'
                }
            }
        }
    }

    distTar {
        baseName = "JGR"
        version = null
        compression = Compression.GZIP
    }

    task copyJGR(type: Copy) {
        from file("${project(':jgr-java').buildDir}/libs/JGR.jar")
        into javaDist
    }

    distZip.enabled false

    copyJGR.dependsOn project(":jgr-java").build
    distTar.dependsOn copyJGR, prepareJGR
    build.dependsOn distTar
}

project(":ibase") {
}

project(":REngine") {
    sourceSets {
        main {
            java {
                srcDirs = [projectDir]
                exclude '**/Rserve/*', '**/JRI/test/*'
            }
        }
    }

    idea {
        module {
            contentRoot = file(projectDir)
            sourceDirs += file(projectDir)
            excludeDirs += file('Rserve')
            excludeDirs += file('JRI/test')
        }
    }

    dependencies {
        compile project(":rJava")
    }
    javadoc.enabled false
    sourcesJar.enabled false
}

project(":rJava") {
    sourceSets {
        main {
            java {
                srcDirs = ['jri']
                exclude '**/bootstrap/**', '**/examples/**', '**/tools/**', '**/REngine/**'
            }
        }
    }

    idea {
        module {
            excludeDirs += file('jri/bootstrap')
            excludeDirs += file('jri/examples')
            excludeDirs += file('jri/tools')
            excludeDirs += file('jri/src')
            excludeDirs += file('jri/REngine')
        }
    }

    javadoc.enabled false
    sourcesJar.enabled false
}


task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
    distributionUrl = 'https://services.gradle.org/distributions/gradle-4.1-all.zip'
}

idea {
    targetVersion = '16'
}
